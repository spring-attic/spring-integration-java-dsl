description = 'Spring Integration Java DSL'

apply plugin: 'java'
apply from: "${rootProject.projectDir}/publish-maven.gradle"
apply plugin: 'eclipse'
apply plugin: 'idea'

buildscript {
	repositories {
		maven { url 'http://repo.spring.io/plugins-release' }
	}
	dependencies {
		classpath 'org.springframework.build.gradle:spring-io-plugin:0.0.3.RELEASE'
	}
}

group = 'org.springframework.integration'

repositories {
	if (version.endsWith('BUILD-SNAPSHOT') || project.hasProperty('platformVersion')) {
		maven { url 'http://repo.spring.io/libs-snapshot' }
	}
	maven { url 'http://repo.spring.io/libs-milestone' }
//	maven { url 'http://repo.spring.io/libs-staging-local' }
}

if (project.hasProperty('platformVersion')) {
	apply plugin: 'spring-io'

	dependencies {
		springIoVersions "io.spring.platform:platform-versions:${platformVersion}@properties"
	}
}

compileJava {
	sourceCompatibility = 1.6
	targetCompatibility = 1.6
}

compileTestJava {
	sourceCompatibility = 1.8
}

ext {
	activeMqVersion = '5.11.0'
	apacheSshdVersion = '0.13.0'
	commonsDbcpVersion = '1.4'
	embedMongoVersion = '1.46.4'
	ftpServerVersion = '1.0.6'
	hsqldbVersion = '2.3.2'
	jacocoVersion = '0.7.2.201409121644'
	jmsApiVersion = '1.1-rev-1'
	jrubyVersion = '1.7.15'
	jythonVersion = '2.5.3'
	kafkaVersion = '0.8.1.1'
	mailVersion = '1.5.2'
	scalaVersion = '2.10'
	slf4jVersion = '1.7.8'
	springIntegrationVersion = '4.1.3.RELEASE'
	springIntegrationKafkaVersion = '1.1.1.RELEASE'
	springBootVersion = '1.2.3.RELEASE'

	linkHomepage = 'https://github.com/spring-projects/spring-integration-java-dsl'
	linkCi = 'https://build.spring.io/browse/INTEXT-SIJD'
	linkIssue = 'https://jira.spring.io/browse/INTEXT'
	linkScmUrl = 'https://github.com/spring-projects/spring-integration-java-dsl'
	linkScmConnection = 'https://github.com/spring-projects/spring-integration-java-dsl.git'
	linkScmDevConnection = 'git@github.com:spring-projects/spring-integration-java-dsl.git'
}

eclipse.project.natures += 'org.springframework.ide.eclipse.core.springnature'

// See http://www.gradle.org/docs/current/userguide/dependency_management.html#sub:configurations
// and http://www.gradle.org/docs/current/dsl/org.gradle.api.artifacts.ConfigurationContainer.html
configurations {
	jacoco //Configuration Group used by Sonar to provide Code Coverage using JaCoCo
}

dependencies {
	compile "org.springframework.integration:spring-integration-core:$springIntegrationVersion"

	['spring-integration-amqp'
	 , 'spring-integration-event'
	 , 'spring-integration-feed'
	 , 'spring-integration-ftp'
	 , 'spring-integration-gemfire'
	 , 'spring-integration-groovy'
	 , 'spring-integration-http'
	 , 'spring-integration-jdbc'
	 , 'spring-integration-jms'
	 , 'spring-integration-jmx'
	 , 'spring-integration-jpa'
	 , 'spring-integration-mail'
	 , 'spring-integration-mongodb'
	 , 'spring-integration-mqtt'
	 , 'spring-integration-redis'
	 , 'spring-integration-rmi'
	 , 'spring-integration-sftp'
	 , 'spring-integration-stream'
	 , 'spring-integration-syslog'
	 , 'spring-integration-twitter'
	 , 'spring-integration-ws'
	 , 'spring-integration-xml'
	 , 'spring-integration-xmpp'].each {
		compile("org.springframework.integration:$it:$springIntegrationVersion", optional)
	}
	compile("org.springframework.integration:spring-integration-kafka:$springIntegrationKafkaVersion") {
		optional it
		exclude group: 'org.apache.avro', module: 'avro'
		exclude group: 'org.apache.avro', module: 'avro-compiler'
		exclude group: 'com.yammer.metrics', module: 'metrics-core'
		exclude group: 'com.yammer.metrics', module: 'metrics-annotation'
		exclude group: 'org.apache.velocity', module: 'velocity'
		exclude group: 'log4j', module: 'log4j'
	}
	compile("javax.jms:jms-api:$jmsApiVersion", provided)
	compile("javax.mail:javax.mail-api:$mailVersion", provided)

	testCompile "org.springframework.integration:spring-integration-test:$springIntegrationVersion"
	testCompile("org.springframework.boot:spring-boot-starter:$springBootVersion") {
		exclude module: 'spring-boot-starter-logging'
	}
	testCompile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
	testCompile "de.flapdoodle.embed:de.flapdoodle.embed.mongo:$embedMongoVersion"
	testCompile "org.apache.ftpserver:ftpserver-core:$ftpServerVersion"
	testCompile "org.apache.sshd:sshd-core:$apacheSshdVersion"
	testCompile ("org.apache.kafka:kafka_$scalaVersion:$kafkaVersion:test") {
		exclude group: 'log4j', module: 'log4j'
	}

	testRuntime "org.slf4j:slf4j-log4j12:$slf4jVersion"
	testRuntime "org.apache.activemq:activemq-broker:$activeMqVersion"
	testRuntime "com.sun.mail:javax.mail:$mailVersion"
	testRuntime "com.sun.mail:smtp:$mailVersion"
	testRuntime "com.sun.mail:pop3:$mailVersion"
	testRuntime "com.sun.mail:imap:$mailVersion"
	testRuntime "org.hsqldb:hsqldb:$hsqldbVersion"
	testRuntime "commons-dbcp:commons-dbcp:$commonsDbcpVersion"
	testRuntime "org.jruby:jruby:$jrubyVersion"
	testRuntime "org.python:jython-standalone:$jythonVersion"

	jacoco "org.jacoco:org.jacoco.agent:$jacocoVersion:runtime"
}

// enable all compiler warnings; individual projects may customize further
[compileJava, compileTestJava]*.options*.compilerArgs = ['-Xlint:all,-options']

test {
	// suppress all console output during testing unless running `gradle -i`
	logging.captureStandardOutput(LogLevel.INFO)
	jvmArgs "-javaagent:${configurations.jacoco.asPath}=destfile=${buildDir}/jacoco.exec,includes=*"
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

apply plugin: 'sonar-runner'

sonarRunner {
	sonarProperties {
		property "sonar.jacoco.reportPath", "${buildDir.name}/jacoco.exec"
		property "sonar.links.homepage", linkHomepage
		property "sonar.links.ci", linkCi
		property "sonar.links.issue", linkIssue
		property "sonar.links.scm", linkScmUrl
		property "sonar.links.scm_dev", linkScmDevConnection
		property "sonar.java.coveragePlugin", "jacoco"
	}
}

javadoc {
	group = 'Documentation'
	description = 'Generates the Javadoc API documentation.'
	title = "${rootProject.description} ${version} API"
	options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
	options.author = true
	options.header = rootProject.description
	options.overview = 'src/api/overview.html'
	options.stylesheetFile = file("src/api/stylesheet.css")

	source = sourceSets.main.allJava
	classpath = project.sourceSets.main.compileClasspath
	destinationDir = new File(buildDir, "api")
}

task docsZip(type: Zip) {
	group = 'Distribution'
	classifier = 'docs'
	description = "Builds -${classifier} archive containing the api " +
			"for deployment at static.springframework.org/spring-integration/docs."

	from(javadoc) {
		into 'api'
	}
}

task distZip(type: Zip, dependsOn: docsZip) {
	group = 'Distribution'
	classifier = 'dist'
	description = "Builds -${classifier} archive, containing all jars and docs, " +
			"suitable for community download page."

	ext.baseDir = "${project.name}-${project.version}";

	from('src/dist') {
		include 'license.txt'
		include 'notice.txt'
		into "${baseDir}"
	}

	from(zipTree(docsZip.archivePath)) {
		into "${baseDir}/docs"
	}

	into("${baseDir}/libs") {
		from project.jar
		from project.sourcesJar
		from project.javadocJar
	}
}

// Create an optional "with dependencies" distribution.
// Not published by default; only for use when building from source.
task depsZip(type: Zip, dependsOn: distZip) { zipTask ->
	group = 'Distribution'
	classifier = 'dist-with-deps'
	description = "Builds -${classifier} archive, containing everything " +
			"in the -${distZip.classifier} archive plus all dependencies."

	from zipTree(distZip.archivePath)

	gradle.taskGraph.whenReady { taskGraph ->
		if (taskGraph.hasTask(":${zipTask.name}")) {
			def projectName = rootProject.name
			def artifacts = new HashSet()

			rootProject.configurations.runtime.resolvedConfiguration.resolvedArtifacts.each { artifact ->
				def dependency = artifact.moduleVersion.id
				if (!projectName.equals(dependency.name)) {
					artifacts << artifact.file
				}
			}

			zipTask.from(artifacts) {
				into "${distZip.baseDir}/deps"
			}
		}
	}
}

artifacts {
	archives distZip
	archives docsZip
}

task dist(dependsOn: assemble) {
	group = 'Distribution'
	description = 'Builds -dist, -docs and distribution archives.'
}

task wrapper(type: Wrapper) {
	description = 'Generates gradlew[.bat] scripts'
	gradleVersion = '2.3'
	distributionUrl = "http://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}
